include ~/toolsets/gcc/gcc-app.hsl
include ~/toolsets/common/utils/lang/c/dependency_scanner.hsl

var src type list;
var inc type list;
var cflags type list;
var lib type list;
var ldflags type list;

project tests : toolset "gcc-c-app" : $src, $inc, $cflags, $lib, $ldflags, "tests";

tests.prologue() {
    forge_libcutest();
    $src.ls(".*\\.c$");
    $inc = hefesto.sys.get_option("includes");
    $cflags = hefesto.sys.get_option("cflags");
    $lib = hefesto.sys.get_option("libraries");
    $ldflags = hefesto.sys.get_option("ldflags");
    if (hefesto.sys.os_name() != "linux") {
        $ldflags.del_item("-ldl");
    }
    get_master_project_objects($ldflags);
}

tests.epilogue() {
    var code type int;
    $code = 1;
    if (hefesto.sys.last_forge_result() == 0) {
        $code = hefesto.sys.run("bin/tests");
    }
    hefesto.sys.exit($code);
}

function forge_libcutest() : result type none {
    var oldcwd type string;
    $oldcwd = hefesto.sys.pwd();
    if (hefesto.sys.cd("cutest/src") == 0) {
        hefesto.sys.echo("FORGE ERROR: unable to find cutest/src subdirectory.\n");
        hefesto.project.abort(1);
    }
    hefesto.sys.forge("cutest", "Forgefile.hsl", "--obj-output-dir=obj --bin-output-dir=lib");
    hefesto.sys.cd($oldcwd);
    if (hefesto.sys.last_forge_result() != 0) {
        hefesto.project.abort(1);
    }
}

function get_master_project_objects(ldflags type list) : result type none {
    var obj_dir type list;
    $obj_dir = hefesto.sys.get_option("master-project-obj-dir");
    if ($obj_dir.count() > 0) {
        var oldcwd type string;
        $oldcwd = hefesto.sys.pwd();
        hefesto.sys.cd($obj_dir.item(0));
        $ldflags.ls(".*\\.o$");
        hefesto.sys.cd($oldcwd);
    } else {
        $ldflags.ls(".*\\.o$");
    }
    var l type int;
    $l = 0;
    while ($l < $ldflags.count()) {
        var item type string;
        $item = $ldflags.item($l);
        if ($item.match(".*main\\.o$") != 0) {
            $ldflags.del_item($item);
            break;
        }
        $l = $l + 1;
    }
    hefesto.sys.byref($ldflags);
}
